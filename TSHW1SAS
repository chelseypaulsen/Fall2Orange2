libname ts 'C:\Users\Steven\Documents\MSA\Analytics Foundations\lab and hw\Time Series';
* libname ts 'C:\Users\Allison\Documents\Time Series';

proc timeseries data=ts.G_3549_clean plots=(series decomp) seasonality=8766;
	var x;
run;

data modelcomp;
*initiating table of model comparison for GOF stats macro to dump results into;
   attrib Model length=$12
          MAPE  length=8
          RMSE  length=8
          NumParm length=8;
run;

%macro GOFstats(ModelName=,DSName=,NumParms=0,
                ActualVar=Actual,ForecastVar=Forecast);
* a macro to output the summary stats of our models;
* BE SURE to change number of parameters, modelname and DSName every time;
*results will be put in work.modelcomp;

data temp_ds;
   attrib Model length=$12
          MAPE  length=8
          NMAPE length=8
          MSE   length=8
          RMSE  length=8
          NMSE  length=8
          NumParm length=8;
   set &DSName end=lastobs;
   retain MAPE MSE NMAPE NMSE 0 NumParm &NumParms;
   Residual=&ActualVar-&ForecastVar;
   /*----  SUM and N functions necessary to handle missing  ----*/
   MAPE=sum(MAPE,100*abs(Residual)/&ActualVar);
   NMAPE=NMAPE+N(100*abs(Residual)/&ActualVar);
   MSE=sum(MSE,Residual**2);
   NMSE=NMSE+N(Residual);
   if (lastobs) then do;
      Model="&ModelName";
      MAPE=MAPE/NMAPE;
      RMSE=sqrt(MSE/NMSE);
      if (NumParm>0) and (NMSE>NumParm) then 
         RMSE=sqrt(MSE/(NMSE-NumParm));
      else RMSE=sqrt(MSE/NMSE);
      output;
   end;
   keep Model MAPE RMSE NumParm;
run;
data work.modelcomp;
	set work.modelcomp temp_ds ;
run;
%mend GOFstats;


data ts.wellnew2;
	*setup of model 2;
	set ts.G_3549_clean;
	pi=constant("pi");
	s1=sin(2*pi*1*_n_/8760);
	c1=cos(2*pi*1*_n_/8760);
	s2=sin(2*pi*2*_n_/8760);
	c2=cos(2*pi*2*_n_/8760);
	s3=sin(2*pi*3*_n_/8760);
	c3=cos(2*pi*3*_n_/8760);
	s4=sin(2*pi*4*_n_/8760);
	c4=cos(2*pi*4*_n_/8760);
	s5=sin(2*pi*5*_n_/8760);
	c5=cos(2*pi*5*_n_/8760);
	s6=sin(2*pi*6*_n_/8760);
	c6=cos(2*pi*6*_n_/8760);
	s7=sin(2*pi*7*_n_/8760);
	c7=cos(2*pi*7*_n_/8760);
	s8=sin(2*pi*8*_n_/8760);
	c8=cos(2*pi*8*_n_/8760);
	s9=sin(2*pi*9*_n_/8760);
	c9=cos(2*pi*9*_n_/8760);
	s10=sin(2*pi*10*_n_/8760);
	c10=cos(2*pi*10*_n_/8760);
	*s11=sin(2*pi*11*_n_/8760);
	*c11=cos(2*pi*11*_n_/8760);
	*s12=sin(2*pi*12*_n_/8760);
	*c12=cos(2*pi*12*_n_/8760);
run;

proc arima data=ts.wellnew2 plot=all;
	*running of model 2;
	identify var=x(8766) crosscorr=(s1 c1 s2 c2 s3 c3 s4 c4 s5 c5 s6 c6 s7 c7 s8 c8 s9 c9 s10 c10);* s11 c11 s12 c12);
	estimate p=4 q=4 input=(s1 c1 s2 c2 s3 c3 s4 c4 s5 c5 s6 c6 s7 c7 s8 c8 s9 c9 s10 c10);* s11 c11 s12 c12); 
	forecast back=2000 lead=2000
		out=work.model2out noprint;
run;
quit;
* calling model output into macro to get summary statistics;
* Again, BE SURE to change number of parameters, modelname and DSName every time;
* results will be put in work.modelcomp;
%GOFstats(ModelName=model2,DSName=work.model2out,NumParms=29,
          ActualVar=x,ForecastVar=Forecast);


data ts.wellnew3;
	*setup of model 3;
	set ts.G_3549_clean;
	pi=constant("pi");

	* daily cycle ;
	s1=sin(2*pi*_n_/24);
	c1=cos(2*pi*_n_/24);

	* tide cycle ;
	s2=sin(2*pi*_n_/12.5);
	c2=cos(2*pi*_n_/12.5);

	* lunar cycle ;
	s3=sin(2*pi*_n_/(27.3*24));
	c3=cos(2*pi*_n_/(27.3*24));

	* yearly cycle ;
	s4=sin(2*pi*_n_/(365.25*24));
	c4=cos(2*pi*_n_/(365.25*24));
	
	* bi-annual cycles;
	s5=sin(2*pi*0.5*_n_/8766);
	c5=cos(2*pi*0.5*_n_/8766);
run;

proc arima data=ts.wellnew3 plot=all;
	*running arima and forecast of model 3;
	identify var=x(1,8766) crosscorr=(s1 c1 s2 c2 s3 c3 s4 c4 s5 c5 ); *putting all sine/cosine fxn in crosscorr and input options;
	estimate p=12 q=4 input=(s1 c1 s2 c2 s3 c3 s4 c4 s5 c5 );
	forecast back=2000 lead=2000
		out=work.model3out noprint;
run;
quit;
* calling model output into macro to get summary statistics;
%GOFstats(ModelName=model3,DSName=work.model3out,NumParms=27,
          ActualVar=x,ForecastVar=Forecast);
